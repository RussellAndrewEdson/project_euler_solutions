;; Code for Project Euler Problem 37.
;;
;; Code author: Russell A. Edson
;; Date last modified: 16/08/2021

;; In this problem we want to find the truncatable primes: that is,
;; primes in which you can truncate from left-to-right and
;; right-to-left such that you keep getting a prime number as you
;; go, e.g. like 3797:
;;   3797         3797
;;    797         379
;;     97         37
;;      7         3
;; are all primes. We are given that there are only eleven of these
;; numbers (2, 3, 5, 7 don't count as truncatable primes).

;; To start with, we'll code a simple check for prime numbers using
;; a classic trial division algorithm:
(defun is-prime? (n)
  "True if the given number N is prime, False if not."
  (if (= n 1)
      nil
      (let ((primep t))
	(loop for m from 2 upto (floor (sqrt n)) do
	  (if (zerop (mod n m))
	      (setf primep nil)))
	primep)))

(is-prime? 3797)
;;=> T

;; Rather than do any exhaustive search of the problem space
;; (for which we're not even sure what the bounds are), we'll
;; instead build up the solution piece by piece.
;;
;; We know there are eleven such truncatable primes. We're
;; given 3797 for free, and actually we're also given 797 too:
;;   797     797
;;    97     79
;;     7     7
;; since we can show that 79 is prime:
(is-prime? 79)
;;=> T

(defvar truncatable-primes (list 3797 797))

;; Now observe the following. The leftmost and rightmost digits
;; of our truncatable primes can only be 2, 3, 5, or 7, since we
;; must inevitably end at one of those numbers when we truncate.
;; Further, 2 cannot be a rightmost digit: in such a case, we'd
;; end up with a multi-digit number ending in a 2, which
;; would necessarily be divisible by 2 and hence not be prime.
;; The digits 1 and 9 can appear as middle digits in the number,
;; but not as leftmost or rightmost digits (since neither 1 nor 9
;; are prime numbers). No other digits can appear anywhere, since
;; they would give us an even number at some point in the truncation.
;;
;; So this gives us our recipe for building such truncatable primes:
;; rather than loop through some nebulously-defined problem space,
;; we'll build them up from these rules and atoms, and stop when we
;; have the eleven truncatable primes that we know we're to find.
(defvar leftmost (list 2 3 5 7))
(defvar rightmost (list 3 5 7))
(defvar middle (list 1 3 5 7 9))

;; We'll define some functions for manipulating digits of numbers:
(defun digits (number)
  "Return the set of digits in the given NUMBER."
  (mapcar #'digit-char-p (coerce (write-to-string number) 'list)))

(defun combine-digits (digits)
  "Return the number we get by concatenating the given DIGITS together."
  (values (parse-integer (format nil "濑溟玳趔┅┅ㄤ殓轸辈炒旦换骄ū旦ㄣ镯忾铄溟玳趔ㄣ镱扉篝暴┅换骄闯脖换物麇ъ躞翳弩麸溴骈铄骢钽糸镱麸汨邈骘换趄躅汜翎忪痱轫搴ㄤ彐躅轶趄躅汜翎忪瀛痱轫蹇瞟⒃蝓殒轶趄躅汜翎忪痱轫瀣漆祗殒铒舢ㄩㄩ蟓痱轫蹇瞟戾è戾骠趄躅汜翦瞟蜷玷舡趄躅汜翦瞟趄躅汜翎忪瀛痱轫舂祜镳麒殪ㄡ钿趄躅汜翎忪瀛痱轫戾铉翳ㄤ殓轸戾骠趄躅汜翦┅暴戾铉翳ㄤ殓轸蜷玷舡趄躅汜翦┅暴滹箦翩戾骠趄躅汜翦ㄣ镯忾铄溟玳趔ㄣ潋ㄤ殓轸戾骠趄躅汜翦┅┅箦翩蜷玷舡趄躅汜翦ㄣ镯忾铄溟玳趔ㄢ豸灬篝ㄤ殓轸蜷玷舡趄躅汜翦┅┅箦翩趄躅汜翎忪瀛痱轫ㄡ钿ㄩ蟓痱轫蹇戾骠趄躅汜翦ㄩ蟓痱轫蹇蜷玷舡趄躅汜翦┅┅趄躅汜翎忪瀛痱轫濠铋飑ㄩ蟓趄躅汜翎忪瀛痱轫蹇撤狗换骄ㄩ蟓趄躅汜翎忪瀛痱轫蹇饭珐换骄换语秕篝蜥翦琦鏖祆忮狍骘祆秣蠛麇ъ翎脲遽汨镦翳戾骠盹篝换溟玳趔泔礅轭翳屙鏖翳遽汨镦翳蜷玷繇矬溟玳趔犷翳孱徜换轭黹滗戾溟玳趔镶箦蝣翳狒麇鲥犰蝈徜骘躅泔躔戾镦换箔溟玳趄躅汜翎忪痱轫弩鏖翳秕铄邃轭犷黹滗戾溟玳趔弭ㄤ彐鲠汜钿殇狒弩祜镳骘戾骠轭戾骠盹篝狃疱钿祜镳骘蜷玷轭蜷玷繇矬泔祆邈ㄣ镯忾铄溟玳趔扉篝戾骠蜷玷舂┅┅磲疸狎灬礅溽铛礅弪扉篝铛礅弪ㄩ蟓趄躅汜翎忪瀛痱轫蹇铛礅弪┅汜钿殇狒弩换骄è渤冤ú紊泰ú紊泰ǔ紊泰ǔ紊泰ǔ冤ǖ冤换骄ǖ紊泰ǖ紊泰ǚ冤ǚ紊泰ǚ紊泰箦翩趄躅汜翎忪瀛痱轫弩ㄡ痧孱趄躅汜翎忪瀛痱轫弩扉篝渤撤党烦┅趄躅汜翎忪瀛痱轫弩换骄ǔ饭饭渤撤党烦换语麇鲥骘躅趄躅汜翎忪痱轫弩盹蝈戾骠语铒麇徜轭换遽汨镦翳黹滗戾溟玳趔麸弼弪镱镦翳箔溟玳汜钿殇狒弩换麸箦殒麇鲥珏铄蜥翦铄趄躅汜翎忪痱轫弩麇轭沆蹁弼孱换翳矬箔溟玳铛礅弪翳狒麇蝈铒趄躅汜翎忪痱轫弩箝钽瀹绠换贩轶铒痱轫瀣怩轭箦螋轭玳鲥饭麒殂轶趄躅汜翎忪换痱轫濠ㄤ彐躅徜洵轭黹滗戾ī⑴疳钿翳汜钿殇狒弩扉篝怡徜溟铉轭黹滗戾溟玳趔箦翩汜钿殇狒弩祜镳骘铛礅弪轭汜钿殇狒弩狃疱钿祜镳骘黹轭黹滗戾泔祆邈ㄣ镯忾铄溟玳趔ㄡ痧孱扉篝ㄣ狎ㄤ殓轸铛礅弪┅黹洎ㄣ潋ㄤ殓轸铛礅弪┅┅┅┅ㄡ滗轭黹滗戾螬磲疸狎灬礅溽铛礅弪扉篝铛礅弪ㄩ蟓趄躅汜翎忪瀛痱轫蹇铛礅弪┅汜钿殇狒弩换骄è脖紊泰ú吵紊泰ú党紊泰ú烦紊泰ú钩紊泰ú钡紊泰换骄ú车紊泰ú档紊泰ú返紊泰ú沟紊泰ú狈紊泰ú撤紊泰换骄ú捣紊泰ú贩紊泰ú狗紊泰ǔ背冤ǔ吵紊泰ǔ党紊泰ǔ烦冤换骄ǔ钩紊泰ǔ钡紊泰ǔ车紊泰ǔ档紊泰ǔ返紊泰ǔ沟紊泰换骄ǔ狈冤ǔ撤紊泰ǔ捣紊泰ǔ贩紊泰ǔ狗紊泰ǖ背紊泰换骄ǖ吵紊泰ǖ党紊泰ǖ烦紊泰ǖ钩紊泰ǖ钡紊泰ǖ车紊泰换骄ǖ档紊泰ǖ返紊泰ǖ沟紊泰ǖ狈紊泰ǖ撤紊泰ǖ捣紊泰换骄ǖ贩紊泰ǖ狗紊泰ǚ背紊泰ǚ吵紊泰ǚ党紊泰ǚ烦紊泰换骄ǚ钩紊泰ǚ钡紊泰ǚ车紊泰ǚ档紊泰ǚ返紊泰ǚ沟紊泰换骄ǚ狈紊泰ǚ撤紊泰ǚ捣紊泰ǚ贩紊泰ǚ狗冤换语麇鲥骘躅箫礤铄趄躅汜翎忪痱轫弩桢蝈麸锖箦翩趄躅汜翎忪瀛痱轫弩ㄡ痧孱趄躅汜翎忪瀛痱轫弩扉篝潮撤潮珐┅趄躅汜翎忪瀛痱轫弩换骄ǔ饭饭渤撤党烦潮撤潮珐换术篝赭戾骠麸骈钿箫麇ъ蝈疱狒翳痱镢弩徵衢詈ㄡ滗轭黹滗戾螬蝈盹鲥殒铒＇轶趄躅汜翎忪瀛痱轫蹇汜钿殇狒弩换骄ǔ背撤狗箦翩趄躅汜翎忪瀛痱轫弩ㄣ镱潮撤趄躅汜翎忪瀛痱轫弩┅趄躅汜翎忪瀛痱轫弩换骄ǔ背撤狗饭渤撤党烦潮撤潮珐换项盹蝈趄躅汜翎忪痱轫戾骠麸骈钿箫麇顼徵衢詈ㄡ滗轭黹滗戾螬蝈盹鲥殒铒＇轶趄躅汜翎忪瀛痱轫蹇汜钿殇狒弩换骄紊换族溟漕骈钿犷翳狒糸礤箫镱沐盹蝈ㄡ滗轭黹滗戾螬蝈盹鲥殒铒＇轶趄躅汜翎忪瀛痱轫蹇汜钿殇狒弩换骄ǚ彻彻珐换语麇鲥骘躅秕北翳犷灬篝趄躅汜翎忪痱轫瀹族汜铒换篚翳屙躔麸珏翳溴箝蝈蝈篚祠箦翩趄躅汜翎忪瀛痱轫弩ㄣ镱烦钩狗趄躅汜翎忪瀛痱轫弩┅趄躅汜翎忪瀛痱轫弩换骄ǚ彻彻潮撤撤狗饭渤撤党烦潮撤潮珐蝈漉沐＇趄躅汜翎忪瀛痱轫弩换骄反赋狈